import "Srl/Console";
import "Srl/Array";
use Srl;

def INT_MIN: Int =  - 2147483648;

func max(a: Int, b: Int): Int {
    if a > b return a else return b;
}

func maxCrossingSum(arr: ref[Array[Int]], low: Int, mid: Int, high: Int): Int {
    def sum: Int = 0;
    def left_sum: Int = INT_MIN;

    def i: Int;

    for i = mid, i >= low, i-- {
        sum += arr(i);
        if sum > left_sum left_sum = sum;
    }

    sum = 0;
    def right_sum: Int = INT_MIN;

    for i = mid + 1, i <= high, i++ {
        sum += arr(i);
        if sum > right_sum right_sum = sum;
    }

    return max(max(left_sum, right_sum), left_sum + right_sum);
}

func maxSubarraySum(arr: ref[Array[Int]], low: Int, high: Int): Int {
    if low == high return arr(low);

    def mid: Int = (low + high) / 2;

    return max(max(maxSubarraySum(arr, low, mid),
        maxSubarraySum(arr, mid + 1, high)),
        maxCrossingSum(arr, low, mid, high));
}

def arr: Array[Int]({ -2, -3, 4, -1, -2, 1, 5, -3 });
def max_sum: Int = maxSubarraySum(arr, 0, arr.getLength() - 1);
Console.print("Maximum subarray sum is %d\n", max_sum);
